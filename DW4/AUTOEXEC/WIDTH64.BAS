10 REM THIS PROGRAM REQUIRES 64K
20 REM DISK EXTENDED COLOR BASIC
30 REM *************************
40 REM THIS BASIC WRAPPER USES A
50 REM WIDTH64.BIN ML ROUTINE TO
60 REM COPY ROM TO RAM, PATCH IT
70 REM FOR 64 COLUMN TEXT MODE,
80 REM AND ENABLE COCOVGA 64-
90 REM COLUMN TEXT MODE
100 REM ************************
110 REM MEMORY MAP:
120 REM $E00: TEMPORARY CHARSET
130 REM       STORAGE FOR UPLOAD
140 REM       AND 2K VIDEO RAM
150 REM       FOR DISPLAYING 64-
160 REM       COLUMN TEXT
170 REM $6000:COCOVGA REGISTER
180 REM       SHADOW SPACE
190 REM $6100:COCOVGA 64-COLUMN
200 REM       DECB PATCH
210 REM       INSERTION CODE AND
220 REM       HARDWARE CONFIG
230 REM       SOFTWARE
240 REM ************************
250 REM WIDTH64.BIN USR CALL
260 REM TAKES A BITFIELD
270 REM ARGUMENT, WHERE:
280 REM -BIT 0: ENABLE LOWERCASE
290 REM -BIT 1: ENABLE T1 FONT
300 REM -BIT 2: ENABLE CUSTOM
310 REM         CHARACTER SET
320 REM THE CUSTOM CHARACTER SET
330 REM MUST BE UPLOADED TO $E00
340 REM ************************
350 CLEAR 200,&H5FFF
360 PCLEAR 2
370 REM ************************
380 REM ******* DEFAULTS *******
390 L=1 'LOWERCASE ENABLED
400 T=0 'T1 FONT DISABLED
410 C=0 'CUSTOM CHAR SET ENABLED
420 CH$="LOWERKIT.CHR:1" 'CHARSET
430 P=0 'PROMPT USER
440 REM ************************
450 IF P=1 GOSUB 1000
451 CLS
460 LOADM "WIDTH64.BIN:1"
470 LOADM CH$
480 DEFUSR=&H6100
490 V=(C*4) OR (T*2) OR (L*1)
500 A=USR0(V)
501 CLS:PRINT "Type RUN <ENTER> to display CATALOG":PRINT:LOAD"CATLOG64.BAS:1"
510 END
1000 REM ***********************
1010 REM PROMPT USER FOR MODE
1020 REM SELECTION
1030 REM ***********************
1040 PRINT
1050 PRINT "   COCOVGA 64-COLUMN TEXT MODE"
1060 PRINT "      UTILITY FOR 64K DECB"
1070 PRINT
1080 B=L
1090 S$="FORCE LOWERCASE?"
1100 GOSUB 2000
1110 L=B
1120 B=T
1130 S$="ENABLE T1 FONT? "
1140 GOSUB 2000
1150 T=B
1160 B=C
1170 S$="ENABLE SOFTWARE CHARACTER SET?  "
1180 GOSUB 2000
1190 C=B
1200 IF C=0 THEN 1260 'SKIP FILENAME IF NO CHARSET UPLOAD
1210 DIR
1220 PRINT "CHARSET FILENAME TO UPLOAD?"
1230 PRINT "(DEFAULT ";CH$;")";
1240 INPUT I$
1250 IF I$<>"" THEN CH$=I$
1260 REM JUMP TO HERE IF NO CHARSET FILENAME
1270 PRINT
1280 RETURN
2000 REM ***********************
2010 REM HANDLE BOOLEAN OPTIONS
2020 REM ***********************
2030 IF B=0 THEN D$="N" ELSE D$="Y"
2040 j S$;" (DEFAULT ";D$;")";
2050 INPUT I$
2060 IF I$<>"" THEN I$=LEFT$(I$,1) ELSE I$=D$
2070 IF I$="N" THEN B=0
2080 IF I$="Y" THEN B=1
2090 RETURN
